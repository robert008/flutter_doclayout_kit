// The Android Gradle Plugin builds the native code with the Android NDK.

group = "com.robert008.flutter_doclayout_kit"
version = "1.0"

buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        // The Android Gradle Plugin knows how to build native code with the NDK.
        classpath("com.android.tools.build:gradle:8.7.0")
    }
}

rootProject.allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

apply plugin: "com.android.library"

def jniLibsDir = file("src/main/jniLibs")
def arm64Dir = file("src/main/jniLibs/arm64-v8a")
def armeabiDir = file("src/main/jniLibs/armeabi-v7a")
def releaseVersion = "v1.0.0"
def downloadUrl = "https://github.com/robert008/flutter_doclayout_kit/releases/download/${releaseVersion}/jniLibs.zip"

task downloadNativeLibraries {
    doLast {
        if (arm64Dir.exists() && file("${arm64Dir}/libdoc_layout_kit.so").exists()) {
            println "Android native libraries already exist"
            return
        }

        println "Downloading Android native libraries..."
        println "URL: ${downloadUrl}"

        def zipFile = file("${buildDir}/jniLibs.zip")
        zipFile.parentFile.mkdirs()

        ant.get(src: downloadUrl, dest: zipFile, skipexisting: 'false')

        jniLibsDir.mkdirs()
        ant.unzip(src: zipFile, dest: jniLibsDir)

        zipFile.delete()

        println "Android native libraries downloaded successfully"
    }
}

preBuild.dependsOn downloadNativeLibraries

android {
    namespace = "com.robert008.flutter_doclayout_kit"

    compileSdk = 35

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    defaultConfig {
        minSdk = 21
        ndk {
            abiFilters 'arm64-v8a', 'armeabi-v7a'
        }
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['src/main/jniLibs']
        }
    }
}
